define KeyValuePair[K, V](key: K, value: V)

define HashMapBuilder[K, V]
(
    length: size_t (0)
    slots: pointer[Vector[BuilderList[Slot[KeyValuePair[K, V]]]]] (nullptr)
)
{
    init(rp: pointer[Page], vector: Vector[KeyValuePair[K, V]]) (HashMapBuilder[K, V]()) {
        if vector.length > 0
        {
            reallocate(vector.length)
            var vector_iterator VectorIterator[KeyValuePair[K, V]](&vector)
            while let element vector_iterator.next()
                add_internal((*element).key, (*element).value)
        }
    }

    function reallocate(this: HashMapBuilder[K, V], size: size_t) {
        let hash_size hashing->get_prime(size)
        let slots_page (*Page->get(this)).allocate_exclusive_page()
        let new_slots Vector[BuilderList[Slot[KeyValuePair[K, V]]]]^slots_page(slots_page, hash_size)

        if slots <> null
        {
            var vector_iterator VectorIterator[BuilderList[Slot[KeyValuePair[K, V]]]](slots)
            while let element vector_iterator.next()
            {
                var list_iterator BuilderListIterator[Slot[KeyValuePair[K, V]]]((*element).head)
                while let item list_iterator.next()
                {
                    let hash_code (*item).hash_code
                    let slot_number hash_code % (*new_slots).length
                    let slot_list (*(new_slots))[slot_number]
                    (*slot_list).add(slots_page, *item)
                }
            }
            (*Page->get(this)).deallocate_exclusive_page(Page->get(slots))
        }
        set slots: new_slots
    }

    function add(this: HashMapBuilder[K, V], key: K, value: V) returns bool {
        let hash_size hashing->get_prime(length + 1)
        if slots = null || hash_size > (*slots).length
            reallocate(length + 1)
        return add_internal(key, value)
    }

    function add_internal(this: HashMapBuilder[K, V], key: K, value: V) returns bool {
        let hash_code key.hash()
        let slot_number hash_code % (*slots).length
        let slot_list (*(slots))[slot_number]
        var iterator (*slot_list).get_iterator()
        while let item iterator.next()
        {
            if (key.equals((*item).value.key))
                return false
        }

        (*slot_list).add((*slots).get_page(), Slot[KeyValuePair[K, V]] (KeyValuePair<K, V> (key, value), hash_code))

        set length: length + 1
        return true
    }

    function contains(this: HashMapBuilder[K, V], key: K) returns bool {
        if slots = null
            return false

        let hash key.hash()
        let slot_number hash % (*slots).length
        let slot (*slots).get(slot_number)
        var iterator (*slot).get_iterator()
        while let item iterator.next()
        {
            if key.equals((*item).value.key)
                return true
        }

        return false
    }

    operator [](key: K) returns pointer[V]$ {
        if slots = null
            return null

        let slot (*slots).get(key.hash() % (*slots).length)
        var iterator (*slot).get_iterator()
        while let item iterator.next()
        {
            if (key.equals((*item).value.key))
                return &((*item).value.value)
        }

        return nullptr
    }
}
