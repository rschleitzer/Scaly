define HashSet[T]
(
    slots: pointer[Vector[Vector[T]]] (null)
)
{
    init# (rp, hash_set_builder: HashSetBuilder[T])
    {
        if hash_set_builder.length = 0
        {
            set slots: null
            return
        }

        set slots: Vector[Vector[T]]^rp(hash_set_builder.slots.length)
        var length hash_set_builder.slots.length
        var i: size_t 0
        while i < length
        {
            var array Array[T]$()
            let slot_ptr hash_set_builder.slots.get_ptr(i)
            if slot_ptr <> null
            {
                var list_iterator ListIterator[Slot[T]](slot_ptr.head)
                while let item list_iterator.next()
                    array.add(item.value)
            }
            if array.length > 0
                slots.put(i, Vector[T]^rp(*array))

            set i: i + 1
        }
    }

    function contains(this: HashSet[T], value: T) returns bool
    {
        if slots = null
            return false

        let hash value.hash()
        let slot_number hash % slots.length
        let slot_ptr slots.get_ptr(slot_number)
        if slot_ptr = null
            return false
        let slot *slot_ptr
        let length slot.length
        var i: size_t 0
        while i < length
        {
            let item slot.get_ptr(i)
            if item <> null
            {
                if value.equals(*item)
                    return true
            }

            set i: i + 1
        }

        return false
    }
}
