use scaly.memory.Page
use scaly.memory.runtime

define StringBuilder
(
    buffer: Array[char]
)
{
    ; StringBuilder requires page allocation because Array.add() uses Page.get(this)
    ; Use StringBuilder#() to allocate on caller's page

    init# (page)
    {
        set buffer: Array[char]^page()
    }

    init# (page, size: size_t)
    {
        set buffer: Array[char]^page(size)
    }

    init# (page, character: char)
    {
        set buffer: Array[char]^page()
        append(character)
    }

    init# (page, string: String)
    {
        set buffer: Array[char]^page()
        append(string)
    }

    init# (page, c_string: pointer[const_char])
    {
        set buffer: Array[char]^page()
        append(c_string)
    }

    function append(this: StringBuilder, character: char)
        buffer.add(character)

    function append(this: StringBuilder, string: String)
        buffer.add(Vector[char](string.get_buffer(), string.get_length()))

    function append(this: StringBuilder, c_string: pointer[const_char])
    {
        let length strlen(c_string)
        if length = 0
            return

        buffer.add(Vector[char](c_string as pointer[char], length))
    }

    function append(this: StringBuilder, start: pointer[char], length: size_t)
    {
        buffer.add(Vector[char](start, length))
    }

    function get_length(this: StringBuilder) returns size_t
        buffer.get_length()

    function to_string#(page, this: StringBuilder) returns String
        String^page(buffer.get_buffer(), buffer.get_length())
}
