use scaly.memory.Page

define StringBuilder
(
    buffer: Array[char]
)
{
    init ()
    {
        set buffer: Array[char]()
    }

    init (size: size_t)
    {
        set buffer: Array[char](size)
    }

    init (character: char)
    {
        set buffer: Array[char]()
        append(character)
    }

    init (string: String)
    {
        set buffer: Array[char]()
        append(string)
    }

    init (c_string: pointer[const_char])
    {
        set buffer: Array[char]()
        append(c_string)
    }

    function append(this: StringBuilder, character: char) { buffer.add(character) }

    function append(this: StringBuilder, string: String) { buffer.add(Vector[char](string.get_buffer(), string.get_length())) }

    function append(this: StringBuilder, c_string: pointer[const_char])
    {
        let length strlen(c_string)
        if length = 0
            return

        buffer.add(Vector[char](c_string as pointer[char], length))
    }

    function append(this: StringBuilder, start: pointer[char], length: size_t)
    {
        buffer.add(Vector[char](start, length))
    }

    function get_length(this: StringBuilder) returns size_t { buffer.get_length() }

    function to_string(this: StringBuilder, rp: pointer[Page]) returns String { String(rp, buffer.get_buffer(), buffer.get_length()) }
}
