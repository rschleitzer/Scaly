use scaly.memory.Page

define containers {
    module hashing
    module Slice
    module Node
    module ListIterator
    module List
    module Vector
    module Array
    module BuilderListIterator
    module BuilderList
    module Slot
    module KeyValuePair
    module HashSetBuilder
    module HashSet
    module HashMapBuilder
    module HashMap
    module String
    module StringIterator
    module StringBuilder

    function test_vector() returns int {
        var rp Page.allocate_page()
        var vector Vector[int]^rp(rp, 2)
        set *(*vector).get(0): 1
        (*vector).put(1, 2)
        if *((*vector).get(0)) <> 1
            return -1
        if *(*vector)[1] <> 2
            return -2
        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test_array() returns int {
        var array Array[int]$()

        ; A quarter gigabyte worth of ints
        let huge_number: int 1024 * 1024 * 64
        {
            var i: int 1
            while i <= huge_number
            {
                (*array).add(i)
                set i: i + 1
            }
        }

        let buffer: pointer[int] (*array).get_buffer()
        {
            var i: int 1
            while i <= huge_number
            {
                if *(buffer + i - 1) <> i
                    return -6
                set i: i + 1
            }
        }
        return 0
    }

    function test_string_simple() returns int {
        var rp Page.allocate_page()
        var s String(rp, "Hello world!")
        let length s.get_length()
        if length <> 12
            return -100
        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test_string() returns int {
        var rp Page.allocate_page()
        var string String(rp, "Hello world!")
        let length string.get_length()
        if length <> 12
            return -7
        var long_string String(rp, "1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890")
        if long_string.get_length() <> 130
            return -8
        var c_string long_string.to_c_string(rp)
        var string_c String(rp, c_string)

        var semi String(rp, ";")
        var dot String(rp, ".")
        if semi.equals(dot)
            return -9
        var semi2 String(rp, ";")
        if semi.equals(semi2) <> true
            return -10
        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test_string_builder() returns int {
        var rp Page.allocate_page()
        var string_builder StringBuilder$()
        let length (*string_builder).get_length()
        if length <> 0
            return -11
        (*string_builder).append('a')
        (*string_builder).append('b')
        (*string_builder).append('c')
        if (*string_builder).to_string(rp).equals("abc") = false
            return -12
        if (*string_builder).get_length() <> 3
            return -13

        var string_builder2 StringBuilder$('d')
        (*string_builder2).append('e')
        (*string_builder2).append('f')
        (*string_builder2).append('g')
        if (*string_builder2).to_string(rp).equals("defg") = false
            return -14
        if (*string_builder2).get_length() <> 4
            return -15

        var string_builder3 StringBuilder$("hijk")
        (*string_builder3).append("lmno")
        if (*string_builder3).to_string(rp).equals("hijklmno") = false
            return -16
        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test_list() returns int {
        var list List[int]$()

        let huge_number: int 1024 * 1024 * 62
        {
            var i: int 1
            while i <= huge_number
            {
                (*list).add(i)
                set i: i + 1
            }
        }

        var iterator (*list).get_iterator()
        {
            var i: int huge_number
            while i >= 1
            {
                let p iterator.next()
                if p = null
                    return -17
                if *p <> i
                    return -18
                set i: i - 1
            }
        }
        return 0
    }

    function test_hash_set() returns int {
        var rp Page.allocate_page()
        var array Array[String]$()
        (*array).add(String(rp, "using"))
        (*array).add(String(rp, "namespace"))
        (*array).add(String(rp, "typedef"))
        let vector Vector[String](rp, *array)
        var keywords_builder HashSetBuilder[String]$(vector)
        if (*keywords_builder).contains(String(rp, "using")) = false
            return -19
        if (*keywords_builder).contains(String(rp, "namespace")) = false
            return -20
        if (*keywords_builder).contains(String(rp, "typedef")) = false
            return -21
        if (*keywords_builder).contains(String(rp, "nix"))
            return -22
        var keywords HashSet[String](rp, *keywords_builder)
        if keywords.contains(String(rp, "using")) = false
            return -23
        if keywords.contains(String(rp, "namespace")) = false
            return -24
        if keywords.contains(String(rp, "typedef")) = false
            return -25
        if keywords.contains(String(rp, "nix"))
            return -26
        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test_hash_map() returns int {
        {
            var rp Page.allocate_page()
            var array Array[KeyValuePair[String, int]]$()
            (*array).add(KeyValuePair[String, int](String(rp, "using"), 1))
            (*array).add(KeyValuePair[String, int](String(rp, "namespace"), 2))
            (*array).add(KeyValuePair[String, int](String(rp, "typedef"), 3))
            let vector Vector[KeyValuePair[String, int]](rp, *array)
            var keywords_builder HashMapBuilder[String, int]$(rp, vector)
            if (*keywords_builder).contains(String(rp, "using")) = false
                return -27
            if (*keywords_builder).contains(String(rp, "namespace")) = false
                return -28
            if (*keywords_builder).contains(String(rp, "typedef")) = false
                return -29
            if (*keywords_builder).contains(String(rp, "nix"))
                return -30
            if *(*keywords_builder)[String(rp, "using")] <> 1
                return -31
            if *(*keywords_builder)[String(rp, "namespace")] <> 2
                return -32
            if *(*keywords_builder)[String(rp, "typedef")] <> 3
                return -33
            if (*keywords_builder)[String(rp, "nix")] <> null
                return -34
            var keywords HashMap[String, int](rp, *keywords_builder)
            if keywords.contains(String(rp, "using")) = false
                return -35
            if keywords.contains(String(rp, "namespace")) = false
                return -36
            if keywords.contains(String(rp, "typedef")) = false
                return -37
            if keywords.contains(String(rp, "nix"))
                return -38
            if *keywords[String(rp, "using")] <> 1
                return -39
            if *keywords[String(rp, "namespace")] <> 2
                return -40
            if *keywords[String(rp, "typedef")] <> 3
                return -41
            if keywords[String(rp, "nix")] <> null
                return -42
            (*rp).deallocate_extensions()
            free(rp as pointer[void])
        }
        {
            var rp Page.allocate_page()
            var map HashMapBuilder[String, size_t]$()
            var hash_set HashSetBuilder[String]$()
            {
                var i: char 'A'
                while i <= 'Z'
                {
                    var j: char 'a'
                    while j <= 'z'
                    {
                        var k: char '0'
                        while k <= '9'
                        {
                            var l: char '!'
                            while l <= '/'
                            {
                                var rp2 Page.allocate_page()
                                var sb StringBuilder^rp2(i)
                                (*sb).append(j)
                                (*sb).append(k)
                                (*sb).append(l)
                                (*map).add((*sb).to_string(rp), (i * j * k * l) as size_t)
                                (*hash_set).add((*sb).to_string(rp))
                                (*rp2).deallocate_extensions()
                                free(rp2 as pointer[void])

                                set l: l + 1
                            }
                            set k: k + 1
                        }
                        set j: j + 1
                    }
                    set i: i + 1
                }
            }

            {
                var i: char 'A'
                while i <= 'Z'
                {
                    var j: char 'a'
                    while j <= 'z'
                    {
                        var k: char '0'
                        while k <= '9'
                        {
                            var l: char '!'
                            while l <= '/'
                            {
                                var rp2 Page.allocate_page()
                                var sb StringBuilder^rp2(i)
                                (*sb).append(j)
                                (*sb).append(k)
                                (*sb).append(l)
                                let theString (*sb).to_string(rp2)
                                if (*hash_set).contains(theString) = false
                                    return -43
                                if (*map).contains(theString) = false
                                    return -44
                                if *(*map)[theString] <> (i * j * k * l) as size_t
                                    return -45
                                (*rp2).deallocate_extensions()
                                free(rp2 as pointer[void])

                                set l: l + 1
                            }
                            set k: k + 1
                        }
                        set j: j + 1
                    }
                    set i: i + 1
                }
            }
            (*rp).deallocate_extensions()
            free(rp as pointer[void])
        }
        return 0
    }

    function test_slice() returns int {
        var rp Page.allocate_page()

        ; Test Vector slice
        var vector Vector[int]^rp(rp, 5)
        set *(*vector).get(0): 10
        set *(*vector).get(1): 20
        set *(*vector).get(2): 30
        set *(*vector).get(3): 40
        set *(*vector).get(4): 50

        let slice (*vector).as_slice()
        if slice.length <> 5
            return -50
        if *slice[0] <> 10
            return -51
        if *slice[4] <> 50
            return -52

        ; Test sub-slice
        let sub slice.subslice(1 as size_t, 4 as size_t)
        if sub.length <> 3
            return -53
        if *sub[0] <> 20
            return -54
        if *sub[2] <> 40
            return -55

        ; Test String slice
        var s String(rp, "Hello")
        let str_slice s.as_slice()
        if str_slice.length <> 5
            return -56
        if *str_slice[0] <> 'H'
            return -57
        if *str_slice[4] <> 'o'
            return -58

        ; Test slice equality
        var s2 String(rp, "Hello")
        let str_slice2 s2.as_slice()
        if str_slice == str_slice2 = false
            return -59

        (*rp).deallocate_extensions()
        free(rp as pointer[void])
        return 0
    }

    function test() returns int {
        var result test_slice()
        if result <> 0
            return result
        set result: test_string_simple()
        if result <> 0
            return result
        set result: test_vector()
        if result <> 0
            return result
        set result: test_array()
        if result <> 0
            return result
        set result: test_string()
        if result <> 0
            return result
        set result: test_string_builder()
        if result <> 0
            return result
        set result: test_list()
        if result <> 0
            return result
        set result: test_hash_set()
        if result <> 0
            return result
        set result: test_hash_map()
        if result <> 0
            return result
        return 0
    }
}
