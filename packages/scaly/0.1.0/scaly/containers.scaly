use scaly.memory.Page

define containers
{
    module hashing
    module Slice
    module Node
    module ListIterator
    module List
    module Vector
    module Array
    module BuilderListIterator
    module BuilderList
    module Slot
    module KeyValuePair
    module HashSetBuilder
    module HashSet
    module HashMapBuilder
    module HashMap
    module String
    module StringIterator
    module StringBuilder
    module Result

    function test_vector() returns int
    {
        var vector Vector[int]$(2)
        set *vector.get(0): 1
        vector.put(1, 2)
        if *(vector.get(0)) <> 1
            return -1
        if *(*vector)[1] <> 2
            return -2
        0
    }

    function test_array() returns int
    {
        var array Array[int]$()

        ; A quarter gigabyte worth of ints
        let huge_number: int 1024 * 1024 * 64
        {
            var i: int 1
            while i <= huge_number
            {
                array.add(i)
                set i: i + 1
            }
        }

        let buffer: pointer[int] array.get_buffer()
        {
            var i: int 1
            while i <= huge_number
            {
                if *(buffer + i - 1) <> i
                    return -6
                set i: i + 1
            }
        }
        0
    }

    function test_string_simple() returns int
    {
        var s String$("Hello world!")
        let length s.get_length()
        if length <> 12
            return -100
        0
    }

    function test_string() returns int
    {
        var string String$("Hello world!")
        let length string.get_length()
        if length <> 12
            return -7
        var long_string String$("1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890")
        if long_string.get_length() <> 130
            return -8

        var semi String$(";")
        var dot String$(".")
        if semi.equals(dot)
            return -9
        var semi2 String$(";")
        if semi.equals(semi2) <> true
            return -10
        0
    }

    function test_string_builder() returns int
    {
        var string_builder StringBuilder$()
        let length string_builder.get_length()
        if length <> 0
            return -11
        string_builder.append("a" as char)
        string_builder.append("b" as char)
        string_builder.append("c" as char)
        if string_builder.to_string$().equals("abc") = false
            return -12
        if string_builder.get_length() <> 3
            return -13

        var string_builder2 StringBuilder$("d" as char)
        string_builder2.append("e" as char)
        string_builder2.append("f" as char)
        string_builder2.append("g" as char)
        if string_builder2.to_string$().equals("defg") = false
            return -14
        if string_builder2.get_length() <> 4
            return -15

        var string_builder3 StringBuilder$("hijk")
        string_builder3.append("lmno")
        if string_builder3.to_string$().equals("hijklmno") = false
            return -16
        0
    }

    function test_list() returns int
    {
        var list List[int]$()

        let huge_number: int 1024 * 1024 * 62
        {
            var i: int 1
            while i <= huge_number
            {
                list.add(i)
                set i: i + 1
            }
        }

        var iterator list.get_iterator()
        {
            var i: int huge_number
            while i >= 1
            {
                let p iterator.next()
                if p = null
                    return -17
                if *p <> i
                    return -18
                set i: i - 1
            }
        }
        0
    }

    function test_hash_set() returns int
    {
        var array Array[String]$()
        array.add(String$("using"))
        array.add(String$("namespace"))
        array.add(String$("typedef"))
        let vector Vector[String]$(*array)
        var keywords_builder HashSetBuilder[String]$(vector)
        if keywords_builder.contains(String$("using")) = false
            return -19
        if keywords_builder.contains(String$("namespace")) = false
            return -20
        if keywords_builder.contains(String$("typedef")) = false
            return -21
        if keywords_builder.contains(String$("nix"))
            return -22
        var keywords HashSet[String]$(*keywords_builder)
        if keywords.contains(String$("using")) = false
            return -23
        if keywords.contains(String$("namespace")) = false
            return -24
        if keywords.contains(String$("typedef")) = false
            return -25
        if keywords.contains(String$("nix"))
            return -26
        0
    }

    function test_hash_map() returns int
    {
        {
            var array Array[KeyValuePair[String, int]]$()
            array.add(KeyValuePair[String, int](String$("using"), 1))
            array.add(KeyValuePair[String, int](String$("namespace"), 2))
            array.add(KeyValuePair[String, int](String$("typedef"), 3))
            let vector Vector[KeyValuePair[String, int]]$(*array)
            var keywords_builder HashMapBuilder[String, int]$(vector)
            if keywords_builder.contains(String$("using")) = false
                return -27
            if keywords_builder.contains(String$("namespace")) = false
                return -28
            if keywords_builder.contains(String$("typedef")) = false
                return -29
            if keywords_builder.contains(String$("nix"))
                return -30
            if *(*keywords_builder)[String$("using")] <> 1
                return -31
            if *(*keywords_builder)[String$("namespace")] <> 2
                return -32
            if *(*keywords_builder)[String$("typedef")] <> 3
                return -33
            if (*keywords_builder)[String$("nix")] <> null
                return -34
            var keywords HashMap[String, int]$(*keywords_builder)
            if keywords.contains(String$("using")) = false
                return -35
            if keywords.contains(String$("namespace")) = false
                return -36
            if keywords.contains(String$("typedef")) = false
                return -37
            if keywords.contains(String$("nix"))
                return -38
            if *keywords[String$("using")] <> 1
                return -39
            if *keywords[String$("namespace")] <> 2
                return -40
            if *keywords[String$("typedef")] <> 3
                return -41
            if keywords[String$("nix")] <> null
                return -42
        }
        {
            var map HashMapBuilder[String, size_t]$()
            var hash_set HashSetBuilder[String]$()
            {
                var i: char = "A" as char
                while i <= "Z" as char
                {
                    var j: char = "a" as char
                    while j <= "z" as char
                    {
                        var k: char = "0" as char
                        while k <= "9" as char
                        {
                            var l: char = "!" as char
                            while l <= "/" as char
                            {
                                var sb StringBuilder$(i)
                                sb.append(j)
                                sb.append(k)
                                sb.append(l)
                                map.add(sb.to_string$(), (i * j * k * l) as size_t)
                                hash_set.add(sb.to_string$())

                                set l: l + 1
                            }
                            set k: k + 1
                        }
                        set j: j + 1
                    }
                    set i: i + 1
                }
            }

            {
                var i: char = "A" as char
                while i <= "Z" as char
                {
                    var j: char = "a" as char
                    while j <= "z" as char
                    {
                        var k: char = "0" as char
                        while k <= "9" as char
                        {
                            var l: char = "!" as char
                            while l <= "/" as char
                            {
                                var sb StringBuilder$(i)
                                sb.append(j)
                                sb.append(k)
                                sb.append(l)
                                let theString sb.to_string$()
                                if hash_set.contains(theString) = false
                                    return -43
                                if map.contains(theString) = false
                                    return -44
                                if *(*map)[theString] <> (i * j * k * l) as size_t
                                    return -45

                                set l: l + 1
                            }
                            set k: k + 1
                        }
                        set j: j + 1
                    }
                    set i: i + 1
                }
            }
        }
        0
    }

    function test_slice() returns int
    {
        ; Test Vector slice
        var vector Vector[int]$(5)
        set *vector.get(0): 10
        set *vector.get(1): 20
        set *vector.get(2): 30
        set *vector.get(3): 40
        set *vector.get(4): 50

        let slice vector.as_slice()
        if slice.length <> 5
            return -50
        if *slice[0] <> 10
            return -51
        if *slice[4] <> 50
            return -52

        ; Test sub-slice
        let sub slice.subslice(1, 4)
        if sub.length <> 3
            return -53
        if *sub[0] <> 20
            return -54
        if *sub[2] <> 40
            return -55

        ; Test String slice
        var s String$("Hello")
        let str_slice s.as_slice()
        if str_slice.length <> 5
            return -56
        if *str_slice[0] <> "H" as char
            return -57
        if *str_slice[4] <> "o" as char
            return -58

        ; Test slice equality
        var s2 String$("Hello")
        let str_slice2 s2.as_slice()
        if str_slice == str_slice2 = false
            return -59

        ; Test triple chained method calls: as_slice().subslice().subslice()
        let triple_chain vector.as_slice().subslice(0, 4).subslice(1, 3)
        if triple_chain.length <> 2
            return -60
        if *triple_chain[0] <> 20
            return -61
        if *triple_chain[1] <> 30
            return -62

        0
    }

    function test() returns int
    {
        var result test_slice()
        if result <> 0
            return result
        set result: test_string_simple()
        if result <> 0
            return result
        set result: test_vector()
        if result <> 0
            return result
        set result: test_array()
        if result <> 0
            return result
        set result: test_string()
        if result <> 0
            return result
        set result: test_string_builder()
        if result <> 0
            return result
        set result: test_list()
        if result <> 0
            return result
        set result: test_hash_set()
        if result <> 0
            return result
        set result: test_hash_map()
        if result <> 0
            return result
        set result: test_result()
        if result <> 0
            return result
        set result: test_string_operations()
        if result <> 0
            return result
        0
    }

    function test_string_operations() returns int
    {
        var s String$("Hello, World!")

        ; Test contains(char)
        if s.contains("H" as char) = false
            return -200
        if s.contains("d" as char) = false
            return -201
        if s.contains("X" as char)
            return -202

        ; Test contains(String)
        var hello String$("Hello")
        if s.contains(hello) = false
            return -203
        var world String$("World")
        if s.contains(world) = false
            return -204
        var foo String$("foo")
        if s.contains(foo)
            return -205

        ; Test starts_with
        if s.starts_with(hello) = false
            return -206
        if s.starts_with("Hello")= false
            return -207
        if s.starts_with(world)
            return -208
        if s.starts_with("World")
            return -209

        ; Test ends_with
        if s.ends_with("!") = false
            return -210
        var exclaim String$("!")
        if s.ends_with(exclaim) = false
            return -211
        var ending String$("World!")
        if s.ends_with(ending) = false
            return -212
        if s.ends_with(hello)
            return -213

        ; Test index_of
        if s.index_of("H" as char) <> 0
            return -214
        if s.index_of("," as char) <> 5
            return -215
        if s.index_of("!" as char) <> 12
            return -216
        if s.index_of("X" as char) <> -1
            return -217

        ; Test index_of with start offset
        if s.index_of("o" as char, 0) <> 4
            return -218
        if s.index_of("o" as char, 5) <> 8
            return -219

        0
    }

    function test_result() returns int
    {
        ; Test Result.Ok construction
        let ok_result Result[int, bool].Ok(42)
        if ok_result.is_ok() = false
            return -100

        if ok_result.is_error()
            return -101

        if ok_result.unwrap() <> 42
            return -102

        ; Test Result.Error construction
        let err_result Result[int, bool].Error(true)
        if err_result.is_error() = false
            return -103

        if err_result.is_ok()
            return -104

        if err_result.unwrap_error() <> true
            return -105

        ; Test unwrap_or
        let ok2 Result[int, bool].Ok(10)
        if ok2.unwrap_or(99) <> 10
            return -106

        let err2 Result[int, bool].Error(false)
        if err2.unwrap_or(99) <> 99
            return -107

        0
    }
}
