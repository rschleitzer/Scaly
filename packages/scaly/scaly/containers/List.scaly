define List[T @summary "The element type of the list."]

    @summary "A generic singly linked list."

    @remarks "A list of type T which can collect T elements.
    You can add an element living in the region specified,
    and you can remove an element as well."

(
    head: pointer[Node[T]] (null)
        @summary "The head node of the list."
        @remarks "The head of the list will always be the last element added."
)
{
    function get_head(this: List[T]) returns pointer[T]$
    {
        if head = null
            return null

        return &(*head).element
    }

    function count(this: List[T]) returns size_t
    {
        var list_iterator get_iterator()
        var i: size_t 0
        while list_iterator.next() <> null
            set i: i + 1
        return i
    }

    procedure remove(this: List[T], element: T) returns bool {
        var node head
        var previous_node: pointer[Node[T]] null
        while node <> nullptr
        {
            if (*node).element = element
            {
                if previous_node <> nullptr
                    set (*previous_node).next: (*node).next
                if node = head
                    set head: null
                return true
            }
            set previous_node: node
            set node: (*node).next
        }

        return false
    }

    function get_iterator(this: List[T]) returns ListIterator[T]$
        ListIterator[T](head)

    procedure add(this: List[T], element: T)
    {
        let lp get_page()
        let new_node Node[T]^lp(element, head)
        set head: new_node
    }
}
